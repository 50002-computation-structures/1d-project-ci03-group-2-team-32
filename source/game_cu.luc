module game_cu #(
    COLUMN_DIMENSION = 16d16 : COLUMN_DIMENSION > 0,
    ROW_DIMENSION = 16d16 : ROW_DIMENSION > 0,
    PIXEL_COUNT = 16d256 : PIXEL_COUNT > 0,
    BUFFER_SIZE = 1024 : BUFFER_SIZE > 0
) (
    input clk,  // clock
    input rst,  // reset
    input regfile_rd2[32],
    input bullet_slow_clk_out,
    
    
    //    input red_bullet_btn,
    //    input green_bullet_btn,
    //    input blue_bullet_btn,
    input left_btn,
    input right_btn,
    input red_btn,
    input green_btn,
    input blue_btn,
    //    input start,
    
    //TEST
    //input io_button[5],
    
    
    output alufn_signal[6],
    output asel[3],
    output bsel[3],
    output wdsel[2],
    output regfile_wa[4],
    output regfile_ra1[4],
    output regfile_ra2[4],
    output regfile_we,
    output debug[4]
) {
    enum GameStates {
        IDLE,
        CHECK_RIGHT_BOUND,
        CHECK_LEFT_BOUND,
        MOVE_RIGHT,
        MOVE_LEFT,
        TRIGGER_UPDATE,
        // Bullets
        BULLET_ACTIVE,
        BULLET_X_POS,
        SET_RED_BULLET_COLOR,
        SET_GREEN_BULLET_COLOR,
        SET_BLUE_BULLET_COLOR,
        
        CHECK_BULLET_BOUNDARY,
        BULLET_MOVE, 
        BULLET_INACTIVE
        
    }
    // User input
    //const CLK_FREQ = $is_sim() ? 1000 : 100000000
    //button_conditioner movement_button[5](.clk(5x{{clk}}), #CLK_FREQ(5x{{CLK_FREQ}}))
    //edge_detector movement_button_edge[5](.clk(5x{{clk}}), #RISE(5x{{1}}), #FALL(5x{{0}}))
    .clk(clk){
        edge_detector bullet_slow_clk_edge(#RISE(1),#FALL(0))
        
        .rst(rst){
            dff game_fsm[$width(GameStates)](#INIT(GameStates.IDLE))
        }
    }
    
    
    always {
        bullet_slow_clk_edge.in = bullet_slow_clk_out
        // standard setting unless otherwise overwritten by each case 
        
        alufn_signal = 0
        asel = 0 
        bsel = 0
        regfile_we = 0
        regfile_wa = d6 
        regfile_ra1 = d0
        regfile_ra2 = d0
        wdsel = 0
        
        debug = b0000
        
        if (rst){
            game_fsm.d = GameStates.IDLE
        }
        else{
            
            case(game_fsm.q){ 
                
                GameStates.IDLE: 
                    if (right_btn){     // check movement update x right
                        game_fsm.d = GameStates.CHECK_RIGHT_BOUND
                    }
                    else if(left_btn){
                        game_fsm.d = GameStates.CHECK_LEFT_BOUND 
                    }
                    else if(red_btn){ // check the button color
                        regfile_ra2 = d13
                        if(~regfile_rd2[0]){ // only set bullet_active when bullet NOT active
                            game_fsm.d = GameStates.SET_RED_BULLET_COLOR
                        }
                    }
                    else if(green_btn){ // check the button color
                        regfile_ra2 = d13
                        if(~regfile_rd2[0]){ // only set bullet_active when bullet NOT active
                            game_fsm.d = GameStates.SET_GREEN_BULLET_COLOR
                        }
                    }
                    else if(blue_btn){ // check the button color
                        regfile_ra2 = d13
                        if(~regfile_rd2[0]){ // only set bullet_active when bullet NOT active
                            game_fsm.d = GameStates.SET_BLUE_BULLET_COLOR
                        }
                    } 
                    else{
                        regfile_ra2 = d13
                        if(regfile_rd2[0] & bullet_slow_clk_edge.out){ // bullet is active and edge high
                            game_fsm.d = GameStates.CHECK_BULLET_BOUNDARY
                        }
                        else{
                            game_fsm.d =  GameStates.IDLE
                        }
                    }
                
                GameStates.CHECK_RIGHT_BOUND:
                    alufn_signal = b110101        //CMPLT
                    regfile_ra1 = d0              
                    bsel = b010                    //constant 14 (16-2) 
                    asel = b000                                  
                    regfile_we = 1
                    regfile_wa = d11               // temp check right bound
                    game_fsm.d = GameStates.MOVE_RIGHT
                
                GameStates.CHECK_LEFT_BOUND:
                    alufn_signal = b110101        //CMPLT
                    regfile_ra1 = d0              
                    bsel = b011                    //constant 2 
                    asel = b000                                  
                    regfile_we = 1
                    regfile_wa = d11               // temp check bound
                    game_fsm.d = GameStates.MOVE_LEFT
                
                GameStates.MOVE_RIGHT:
                    regfile_ra2 = d11
                    if (regfile_rd2[0]){
                        alufn_signal = b000000         //ADD
                        regfile_ra1 = d0              
                        bsel = b001                    //1
                        asel = b000                                  
                        regfile_we = 1
                        regfile_wa = d0               // player x
                        game_fsm.d = GameStates.TRIGGER_UPDATE
                    }
                    else{
                        game_fsm.d = GameStates.IDLE
                    }
                
                GameStates.MOVE_LEFT:
                    regfile_ra2 = d11
                    if (~regfile_rd2[0]){
                        alufn_signal = b000001         //SUB
                        regfile_ra1 = d0              
                        bsel = b001                    //1
                        asel = b000                                  
                        regfile_we = 1
                        regfile_wa = d0               // player x
                        game_fsm.d = GameStates.TRIGGER_UPDATE
                    }
                    else{
                        game_fsm.d = GameStates.IDLE
                    }
                
                GameStates.TRIGGER_UPDATE:
                    alufn_signal = b000010         // MUL              
                    bsel = b001                    //1
                    asel = b001                                  
                    regfile_we = 1
                    regfile_wa = d15               // update_ram_flag
                    game_fsm.d = GameStates.IDLE
                
                GameStates.SET_RED_BULLET_COLOR:
                    alufn_signal = b000010        // MUL
                    bsel = b001                     // constant 1
                    asel = b011                     // constant 11             
                    regfile_we = 1
                    regfile_wa = d6                 // write color
                    game_fsm.d = GameStates.BULLET_ACTIVE
                
                GameStates.SET_GREEN_BULLET_COLOR:
                    alufn_signal = b000010        // MUL
                    bsel = b001                     // REGFILE
                    asel = b100                     // constant 1             
                    regfile_we = 1
                    regfile_wa = d6                 // write color
                    game_fsm.d = GameStates.BULLET_ACTIVE
                
                GameStates.SET_BLUE_BULLET_COLOR:
                    alufn_signal = b000010        // MUL
                    bsel = b001                     // REGFILE
                    asel = b101                     // constant 1             
                    regfile_we = 1
                    regfile_wa = d6                 // write color
                    game_fsm.d = GameStates.BULLET_ACTIVE
                
                GameStates.BULLET_ACTIVE:
                    alufn_signal = b000000        //ADD
                    bsel = b001                    // 1
                    asel = b001                     // 0             
                    regfile_we = 1                  // write enabled yes
                    regfile_wa = d13               // bul_active
                    game_fsm.d = GameStates.BULLET_X_POS
                
                GameStates.BULLET_X_POS:
                    alufn_signal = b000010        //MUL 
                    regfile_ra1 = d0              
                    bsel = b001                    //constant 1
                    asel = b000                                  
                    regfile_we = 1
                    regfile_wa = d7               // bullet_x
                    game_fsm.d = GameStates.IDLE
                
                
                GameStates.CHECK_BULLET_BOUNDARY:
                    alufn_signal = b110101        //CMPLT
                    regfile_ra1 = d8              // bullet_y
                    bsel = b001                   //constant 1
                    asel = b000                                  
                    regfile_we = 1
                    regfile_wa = d11               // check boundary
                    game_fsm.d = GameStates.BULLET_MOVE
                
                GameStates.BULLET_MOVE:
                    regfile_ra2 = d11
                    if (~regfile_rd2[0]){// if bullet y > 1
                        alufn_signal = b000001        //SUB
                        regfile_ra1 = d8              
                        bsel = b001                    //constant 1
                        asel = b000                                  
                        regfile_we = 1
                        regfile_wa = d8               // bullet_y
                        game_fsm.d = GameStates.IDLE
                    }
                    else{             //reset bullet _y pos       
                        alufn_signal = b000010        //MUL
                        bsel = b001                    //constant 1 
                        asel = b010                     //constant 12             
                        regfile_we = 1
                        regfile_wa = d8               // bullet_y
                        game_fsm.d = GameStates.BULLET_INACTIVE
                    }
                GameStates.BULLET_INACTIVE:
                    alufn_signal = b000010        //MUL
                    bsel = b001                    
                    asel = b001                     //constant 0             
                    regfile_we = 1
                    regfile_wa = d13               // bul_active
                    game_fsm.d = GameStates.IDLE
            }
        }
    }
}